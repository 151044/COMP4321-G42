plugins {
    id 'java'
}

group = 'hk.ust.comp4321'
version = '1.0'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        resources {
            srcDir "data/"
        }
    }
}

allprojects {
    sourceCompatibility = 1.17
    targetCompatibility = 1.17
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core:5.7.0'
    implementation 'com.formdev:flatlaf:3.2.1'
    implementation 'org.jsoup:jsoup:1.17.2'
    implementation 'org.xerial:sqlite-jdbc:3.45.1.0'
    implementation "org.jooq:jooq:3.19.5"
    implementation "org.slf4j:slf4j-simple:2.0.12"
    implementation group: 'org.apache.opennlp', name: 'opennlp-tools', version: '2.3.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = "full"
    }
    testLogging.showStandardStreams = true
}

tasks.register('dist', Jar) {
    manifest {
        attributes 'Main-Class': "hk.ust.comp4321.Main"
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    with jar
}

tasks.register('dist-visualizer', Jar) {
    manifest {
        attributes 'Main-Class': "hk.ust.comp4321.db.visual.Visualizer"
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName = "COMP4321-G42-Visualizer"
    with jar
}